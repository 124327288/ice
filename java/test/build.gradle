// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

ext.testDir = "${projectDir}/src/main/java/test"
apply from: "slice.gradle"

def hasLambda = org.gradle.internal.jvm.Jvm.current().getJavaVersion().toString().toFloat() >= 1.8f

sourceSets {
    main {
        java {
            if (!hasLambda) {
                exclude '**/lambda'
            }
            exclude 'plugins'
            exclude 'ejb'
        }
    }
}

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
}

if (distJarDir == null) {
    dependencies {
        compile project(':Ice')
        compile project(':Freeze')
        compile project(':IceDiscovery')
        compile project(':IceBox')
        compile project(':Glacier2')
        compile project(':IceStorm')
        compile project(':IceGrid')
        compile project(':test/plugins')
    }
} else {
    println "Using Ice jars installed in ${distJarDir}"
    dependencies {
        compile files("${distJarDir}/Ice${versionSuffix}.jar")
        compile files("${distJarDir}/Freeze${versionSuffix}.jar")
        compile files("${distJarDir}/IceDiscovery${versionSuffix}.jar")
        compile files("${distJarDir}/IceBox${versionSuffix}.jar")
        compile files("${distJarDir}/Glacier2${versionSuffix}.jar")
        compile files("${distJarDir}/IceStorm${versionSuffix}.jar")
        compile files("${distJarDir}/IceGrid${versionSuffix}.jar")
        compile project(':test/plugins')
        compile "com.sleepycat:db:${dbVersion}"
    }
}

jar {
    archiveName = "IceTest.jar"
    destinationDir = new File("$rootProject.projectDir/lib/")
}

clean {
    delete("$rootProject.projectDir/lib/IceTest.jar")
}
