// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

sourceCompatibility = iceSourceCompatibility
targetCompatibility = iceTargetCompatibility

slice {
    java {
        stream {
            args = "--ice --stream"
            files = [file("$sliceDir/Ice/BuiltinSequences.ice")]
        }
        nostream {
        	include = ["${sliceDir}"]
            args = "--ice"
            files = fileTree(dir: "$sliceDir", includes:['Ice/*.ice', 'IceSSL/*.ice'],
            	excludes:['Ice/*F.ice', 'Ice/BuiltinSequences.ice'])
        }
    }
}

apply from: "$rootProject.projectDir/gradle/library.gradle"

// We install the gradle plugin from here because a install task cannot be added
// to the buildSrc gradle file.
task installSlicePlugin(type: Copy) {
    from "${rootProject.projectDir}/buildSrc/build/libs/buildSrc.jar"
    into "${DESTDIR}${prefix}/lib"
    rename { "ice-gradle-plugin-${project.version}.jar" }
}
task installSlicePluginSources(type: Copy) {
    from "${rootProject.projectDir}/buildSrc/build/libs/buildSrc-source.jar"
    into "${DESTDIR}${prefix}/lib"
    rename { "ice-gradle-plugin-${project.version}-source.jar" }
}
task installSlicePluginPom(type: Copy) {
    from "${rootProject.projectDir}/buildSrc/buildSrc.pom"
    into "${DESTDIR}${prefix}/lib"
    rename { "ice-gradle-plugin-${project.version}.pom" }
}
install.dependsOn(installSlicePlugin)
install.dependsOn(installSlicePluginSources)
install.dependsOn(installSlicePluginPom)
