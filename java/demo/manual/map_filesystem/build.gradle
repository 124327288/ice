// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

slice {
	java {
		include = ["${projectDir}", "${sliceDir}"]
		srcDir = '.'
	}
	freezej {
		files = [file("FilesystemDB.ice"),
				 file("${sliceDir}/Ice/Identity.ice")]
		include = ["${projectDir}", "${sliceDir}"]
		args = "--ice"
		dict {
			IdentityFileEntryMap {
				javaType = "FilesystemDB.IdentityFileEntryMap"
				key = "Ice::Identity"
				value = "FilesystemDB::FileEntry"
			}
		}
		dict {
			IdentityDirectoryEntryMap {
				javaType = "FilesystemDB.IdentityDirectoryEntryMap"
				key="Ice::Identity"
                value="FilesystemDB::DirectoryEntry"
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDir '.'
			output.classesDir "${buildDir}/classes"
		}
	}
}

if (useMavenJars) {
	dependencies {
		compile "com.zeroc:ice:${iceVersion}"
		compile "com.zeroc:freeze:${iceVersion}"
		compile 'com.sleepycat:db:5.3.28'
	}
} else if (distJarDir != null) {
	dependencies {
		compile files("${distJarDir}/Ice${versionSuffix}.jar")
		compile files("${distJarDir}/Freeze${versionSuffix}.jar")
		compile 'com.sleepycat:db:5.3.28'
	}
} else {
	dependencies {
		compile project(':Ice')
		compile project(':Freeze')
	}
}

// Create a jar for the client & server which includes everything in the demo.
task clientJar(type: Jar) {
   	baseName = "client"
    from(sourceSets.main.output) {
    	include "**"
    }
}

task serverJar(type: Jar) {
    baseName = "server"
    from(sourceSets.main.output) {
    	include "**"
    }
}

clientJar {
    manifest {
        attributes("Main-Class": "Client")
        attributes("Class-Path": configurations.compile.resolve().collect { it.toURI() }.join(' '))
    }
}

serverJar {
    manifest {
    	attributes("Main-Class": "Server")
        attributes("Class-Path": configurations.compile.resolve().collect { it.toURI() }.join(' '))
    }
}

artifacts {
	archives clientJar, serverJar;
}

// Don't create the default jar.
jar.enabled = false

idea.module {
	excludeDirs -= file(buildDir)
	buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
}
