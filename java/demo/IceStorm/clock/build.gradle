// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

slice {
	java {
		include = ["${projectDir}", "${sliceDir}"]
		srcDir = '.'
	}
}

sourceSets {
	main {
		java {
			srcDir '.'
			output.classesDir "${buildDir}/classes"
		}
	}
}

if (useMavenJars) {
	dependencies {
		compile "com.zeroc:ice:${iceVersion}"
		compile "com.zeroc:icestorm:${iceVersion}"
	}
} else if (distJarDir != null) {
	dependencies {
		compile files("${distJarDir}/Ice${versionSuffix}.jar")
		compile files("${distJarDir}/IceStorm${versionSuffix}.jar")
	}
} else {
	dependencies {
		compile project(':Ice')
		compile project(':IceStorm')
	}
}

// Create a jar for the publisher & subscriber which includes everything in the demo.
task publisherJar(type: Jar) {
	baseName = "publisher"
	from(sourceSets.main.output) {
		include "**"
	}
}

task subscriberJar(type: Jar) {
    baseName = "subscriber"
	from(sourceSets.main.output) {
		include "**"
	}
}

publisherJar {
    manifest {
    	attributes("Main-Class": "Publisher")
        attributes("Class-Path": configurations.compile.resolve().collect { it.toURI() }.join(' '))
    }
}

subscriberJar {
    manifest {
    	attributes("Main-Class": "Subscriber")
    	attributes("Class-Path": configurations.compile.resolve().collect { it.toURI() }.join(' '))
    }
}

artifacts {
	archives publisherJar, subscriberJar;
}

jar.enabled = false

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
}

