// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

['applet', 'async', 'bidir', 'callback', 'hello', 'invoke',
 'interrupt', 'latency', 'minimal', 'multicast', 'nested',
 'plugin', 'properties', 'serialize','session', 'swing',
 'throughput', 'value', 'optional','context'].each {
 	def name = it
	project(":demoIce${it.capitalize()}") {
		slice {
			java {
				include = ["${projectDir}", "${sliceDir}"]
				srcDir = '.'
			}
		}

		sourceSets {
			main {
				java {
					srcDir '.'
					output.classesDir "${buildDir}/classes"
				}
			}
		}

		if (distJarDir == null) {
			dependencies {
				compile project(':Ice')
			}
		} else {
			dependencies {
				compile files("${distJarDir}/Ice${versionSuffix}.jar")
			}
		}

		// Create a jar for the client & server which includes everything in the demo.
		task clientJar(type: Jar) {
 		   baseName = "client"
    		from(sourceSets.main.output) {
        		include "**" 
    		}
		}

		task serverJar(type: Jar) {
 		   baseName = "server"
    		from(sourceSets.main.output) {
        		include "**" 
    		}
		}

		clientJar {
		    manifest {
		    	attributes("Main-Class": "Client")
		        attributes("Class-Path": configurations.compile.resolve().collect { it.toURI() }.join(' '))
		    }
		}

		serverJar {
		    manifest {
		    	attributes("Main-Class": "Server")
		    	attributes("Class-Path": configurations.compile.resolve().collect { it.toURI() }.join(' '))
		    }
		}

		artifacts {
			archives clientJar, serverJar;
		}

		// Don't create the default jar.
		jar.enabled = false

		idea.module {
		    excludeDirs -= file(buildDir)
		    buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
		}
	}
}

project(":demoIceInvoke").slice.java.default.args += " --stream"
