// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

['evictor_filesystem', 'lifecycle', 'printer', 'simple_filesystem'].each {
 	def name = it
	project(":demo/book/${it}") {
		slice {
			java {
				include = ["${projectDir}", "${sliceDir}"]
				srcDir = '.'
			}
		}

		sourceSets {
			main {
				java {
					srcDir '.'
					output.classesDir "${buildDir}/classes"
				}
			}
		}

		if (distJarDir == null) {
			dependencies {
				compile project(':src/Ice')
			}
		} else {
			dependencies {
				compile files("${distJarDir}/Ice.jar")
			}
		}

		if(name == "evictor_filesystem") {
			if (distJarDir == null) {
				dependencies {
					compile project(':src/Freeze')
				}
			} else {
				dependencies {
					compile files("${distJarDir}/Freeze.jar")
					compile 'com.sleepycat:db:5.3.28'
				}
			}
		}

		jar.enabled = false

		idea.module {
		    excludeDirs -= file(buildDir)
		    buildDir.listFiles({d, f ->f != 'generated-src'} as FilenameFilter).each { excludeDirs += it }
		}
	}
}
