// **********************************************************************
//
// Copyright (c) 2003-2014 ZeroC, Inc. All rights reserved.
//
// This copy of Ice is licensed to you under the terms described in the
// ICE_LICENSE file included in this distribution.
//
// **********************************************************************

buildscript {
    repositories {
        maven {
            url 'http://repo.zeroc.com/nexus/content/repositories/releases'
        }
    }
    dependencies {
        classpath group: 'com.zeroc.gradle.plugins', name: 'slice', version: '1.0'
    }
}

if (!project.plugins.findPlugin(com.zeroc.gradle.plugins.slice.SlicePlugin)) {
    project.apply(plugin: com.zeroc.gradle.plugins.slice.SlicePlugin)
}

ext.generatedDir = "$project.buildDir/generated"

try {
	compileJava {
		options.debug = debug
	}
} catch(MissingMethodException ex) {
	// For Android builds: thrown if compileJava isn't available.
}

// Determine the name of the Slice-to-Java translator
def isWindows = false
def slice2java = "slice2java"
def slice2freezej = "slice2freezej"
if (System.properties['os.name'].toLowerCase().contains('windows'))
{
    isWindows = true
    slice2java = "slice2java.exe"
    slice2freezej = "slice2freezej.exe"
}

// First check if ICE_HOME is set and it points at a valid installation
def env = System.getenv()
def iceHome = env['ICE_HOME']
if (iceHome != null && iceHome.length() > 0)
{
	def translator = new File("$iceHome/bin/$slice2java")
	if (!translator.exists())
	{
		throw new GradleException("Unable to find $slice2java in $iceHome, please verify ICE_HOME is " +
								  "properly configured and Ice is correctly installed.")
	}
}

def iceBinDist = true

// First check if we are building against source distribution
def iceDir
if (iceHome != null && iceHome.length() > 0)
{
	iceDir = iceHome
}

if (iceDir == null)
{
	def f1 = new File("/usr/bin/$slice2java")
	if (f1.exists())
	{
		iceDir = "/usr"
	}
}

if (iceDir == null && isWindows)
{
	def progFiles
	def arch1 = env['PROCESSOR_ARCHITECTURE']
	def arch2 = env['PROCESSOR_ARCHITEW6432']
	if (arch1 == "AMD64" || arch1 == "IA64" || arch2 == "AMD64" || arch2 == "IA64")
	{
		progFiles = env['ProgramFiles(x86)']
	}
	else
	{
		progFiles = env['ProgramFiles']
	}
	iceDir = "$progFiles\\ZeroC\\Ice-${project.version}"
}

if (iceDir == null)
{
	def f1 = new File("/Library/Developer/Ice-${project.version}/bin/$slice2java")
	if (f1.exists())
	{
		iceDir = "/Library/Developer/Ice-${project.version}"
	}
}

if (iceDir == null)
{
	def f1 = new File("/opt/Ice-${project.version}/bin/$slice2java")
	if (f1.exists())
	{
		iceDir = "/opt/Ice-${project.version}"
	}
}

if (iceDir == null)
{
	throw new GradleException("Unable to find a valid Ice distribution, please verify ICE_HOME is properly " +
							  "configured and Ice is correctly installed.")
}

slice.slice2java = "$iceDir/bin/$slice2java"
slice.slice2freezej = "$iceDir/bin/$slice2freezej"
slice.iceHome = iceDir
slice.srcDist = false

// Set the slice and java jar directory
if (iceDir == "/usr")
{
	ext.sliceDir = "/usr/share/Ice-${project.version}/slice"
	ext.distJarDir = "/usr/share/java"
}
else
{
	ext.sliceDir = "$iceDir/slice"
	ext.distJarDir = "$iceDir/lib"
}

// Check if we need to append version suffix to jar files
ext.versionSuffix = ""
def iceJar = new File("${distJarDir}/Ice-${project.version}.jar")
if(iceJar.exists())
{
	ext.versionSuffix = "-${project.version}"
}

ext.searchFile = { List<Closure> places, List<String> searchPaths ->
	def dirs = []
	places.each {
		def dir = it()
		if(dir != null) {
			dirs << dir + "/"
		}
	}

    def candidates = searchPaths.collect {
		def path = it
		dirs.collect {
			it.concat(path)
		}
	}.flatten()

	return candidates.find {
		new File(it).exists()
	}
}

def demoJar(name) {
    def taskName = name + "Jar"
	// Create a jar for the client & server which includes everything in the demo.
	def jarTask = tasks.create(name: taskName, type: Jar) {
		version = ""
	   	baseName = name
	    from(sourceSets.main.output) {
	    	include "**"
	    }
	}

	jarTask.manifest {
        attributes("Main-Class": name.capitalize())
        attributes("Class-Path": configurations.runtime.resolve().collect { it.toURI() }.join(' '))
    }


	artifacts {
		archives jarTask
	}
}
ext {
	demoJar = this.&demoJar
}

def localDependency(artifactId) {
	return "com.zeroc:${artifactId}:${project.version}"
}
ext {
	localDependency = this.&localDependency
}

//
// Find BerkeleyDB JAR
//
if(dbHome) {
	ext.dbJar = "${dbHome}/db.jar"
} else {
	def subdirs
	if (isWindows) {
		def thirdpartyhome = env['THIRDPARTY_HOME']
		if (thirdpartyhome == null || thirpartyhome.length() == 0) {
			def progFiles
			def arch1 = env['PROCESSOR_ARCHITECTURE']
			def arch2 = env['PROCESSOR_ARCHITEW6432']
			if (arch1 == "AMD64" || arch1 == "IA64" || arch2 == "AMD64" || arch2 == "IA64") {
				progFiles = env['ProgramFiles(x86)']
			} else {
				progFiles = env['ProgramFiles']
			}
			thirdpartyhome = "${progFiles}/ZeroC/Ice-${project.version}-ThirdParty"
		}

		subdirs = [ "${thirdpartyhome}/lib/" ]
	} else {
		subdirs = [
			"/Library/Developer/Ice-${project.version}-ThirdParty/lib/",
			"/usr/share/java/",
			"/opt/Ice-${project.version}/lib/",
			"/opt/db/lib/",
			"/usr/lib/"
		]
	}

	def candidates = ["db-5.3.28.jar", "db-5.3.21.jar", "db.jar"].collect {
		def dbJarName = it
		subdirs.collect {
			it.concat(dbJarName)
		}
	}.flatten()

	ext.dbJar = candidates.find {
		new File(it).exists()
	}
}

if(!ext.dbJar || !new File(ext.dbJar).exists()) {
	ex.dbJar = ""
}
